import net.masterthought.cucumber.ReportBuilder
import net.masterthought.cucumber.Configuration

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'

// In this section you declare where to find the dependencies of your project
repositories {
    mavenCentral()
    maven {
        url "http://repo.bodar.com/"
    }
}

buildscript {
    repositories {
        mavenCentral()
        maven {
            url "http://repo.bodar.com"
        }
    }

    dependencies {
        classpath 'net.masterthought:cucumber-reporting:2.5.0'
    }
}

// In this section you declare the dependencies for your production and test code
dependencies {
    compile group: 'org.seleniumhq.selenium', name: 'selenium-java', version:'2.53.0'
    compile group: 'com.jayway.restassured', name: 'rest-assured', version: '2.9.0'
    compile group: 'log4j', name: 'log4j', version: '1.2.17'
    compile group: 'net.masterthought', name: 'cucumber-reporting', version: '2.5.0'
    compile group:'com.googlecode.totallylazy', name:'totallylazy', version:'1.85'

    testCompile group: 'info.cukes', name: 'cucumber-java', version: '1.2.4'
    testCompile group: 'info.cukes', name: 'cucumber-testng', version: '1.2.4'
    testCompile group: 'info.cukes', name: 'cucumber-picocontainer', version: '1.2.4'
}

configurations {
    cucumberRuntime {
        extendsFrom testRuntime
    }
}

task runFeatures() {
    dependsOn assemble, compileTestJava
    doLast {
        File file = new File("cucumber-html-report")
        file.delete()
        try {
            javaexec {
                main = "cucumber.api.cli.Main"
                classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
                args = ['--plugin', 'pretty',
                        '--plugin', 'html:build/cucumber-html-report',
                        '--plugin', 'json:build/cucumber.json',
                        '--plugin', 'rerun:build/rerun.txt',
                        '--glue', 'org.fundacionjala.webtesting', 'src/test/resources/features']
                jvmArgs = ["-Dcucumber.options=" + cucumberOptions]
            }
        } catch (Exception e) {
            //In order to avoid FAILURE: Build failed with an exception.
        }
        sleep(5000) // sleep due to cucumber.json is not created too fast
        generateReport("build/cucumber.json", "cucumber-html-report")
    }
}

task cucumberRerun() {
    dependsOn assemble, compileTestJava
    doLast {
        File failedScenariosFile = new File('build/rerun.txt')
        if (!failedScenariosFile.getText().isEmpty()) {
            String[] failedScenariosArray = failedScenariosFile.getText().split(" ")
            String failedScenariosPath = ''
            failedScenariosArray.each {
                String item -> failedScenariosPath += featurePath + item + " "
            }
            try {
                javaexec {
                    main = "cucumber.api.cli.Main"
                    classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
                    args = ['--plugin', 'pretty',
                            '--plugin', 'html:build/retry/cucumber-html-report',
                            '--plugin', 'json:build/retry/cucumber.json',
                            '--plugin', 'rerun:build/retry/rerun.txt',
                            '--glue', 'org.fundacionjala.webtesting']
                    jvmArgs = ["-Dcucumber.options=" + failedScenariosPath]
                }
            } catch (Exception e) {
                //In order to avoid FAILURE: Build failed with an exception.
            }
            sleep(5000) // sleep due to cucumber.json is not created too fast
            generateReport("build/retry/cucumber.json", "cucumber-html-report/retry")
        } else {
            println 'It does not exist failed scenarios'
            throw new StopExecutionException('It does not exist failed scenarios')
        }
    }
}

def generateReport(String pathCucumber, String pathReport) {
    File reportOutputDirectory = new File(pathReport)
    List<String> jsonReportFiles = new ArrayList<>()
    jsonReportFiles.add(pathCucumber)
    String buildProjectName = "cucumber-jvm-rest-assured"
    Configuration configuration = new Configuration(reportOutputDirectory, buildProjectName);
    ReportBuilder reportBuilder = new ReportBuilder(jsonReportFiles, configuration)
    reportBuilder.generateReports()
}
